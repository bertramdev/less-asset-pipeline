apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
version = '2.0.8'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if(!project.hasProperty('bintrayUser')) {
    bintrayUser = 'test'
}
if(!project.hasProperty('bintrayKey')) {
    bintrayKey = 'empty'
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}


repositories {
	mavenLocal()
    jcenter()
    mavenCentral()

}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    provided 'org.codehaus.groovy:groovy-all:2.0.7'
	compile "com.bertramlabs.plugins:asset-pipeline-core:2.0.12"
    compile 'org.mozilla:rhino:1.7R4'
    compile 'log4j:log4j:1.2.17'
	compile "com.github.sommeri:less4j:1.8.4"
    compile "com.github.sommeri:less4j-javascript:0.0.1"

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'less-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'less-asset-pipeline'
                    description 'Provides LESS Compiling support for the Asset-Pipeline library.'
                    url 'https://github.com/bertramdev/less-asset-pipeline'
                    scm {
                        url 'https://github.com/bertramdev/less-asset-pipeline'
                        connection 'scm:https://bertramdev@github.com/bertramdev/less-asset-pipeline.git'
                        developerConnection 'scm:git://github.com/bertramdev/less-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['maven']
    pkg {
        repo = 'asset-pipeline'
        userOrg = 'bertramlabs'
        name = 'less-asset-pipeline'
        licenses = ['Apache-2.0']
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
